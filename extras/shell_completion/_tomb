#compdef _tomb tomb

typeset -A opt_args

function _tomb {
    local line

    _arguments -C \
        '(-)'{-h,--help}'[print help information]' \
        '(-)'{-q,--quietly}'[run quietly]' \
        '(-)'{-v,--version}'[print version information]' \
        '(-)'{-D,--debug}'[print debugging information at runtime]' \
        '(-)'{-f,--force}'[force operation]' \
        "--tmp[what do I do?]" \
        "-U[Switch to this user ID when dropping privileges]" \
        "-G[Switch to this group ID when dropping privileges]" \
        "-T[Switch to this TTY terminal when dropping privileges]" \
        "--no-color[do not use colors]" \
        "--unsafe[man page somewhat unclear; either it enables dev or it needs dev enabled]" \
        "1: :(bury close dig engrave exhume forge help index list lock mount open passwd ps resize search umount setkey slam)" \
        "*::arg:->args"

    case $line[1] in
        bury)
            _tomb_assym
            _tomb_sym
            _tomb_pwd
            _tomb_pwd_old
        ;;
        close|umount)
            _tomb_hooks
        ;;
        dig)
            _tomb_size
            _tomb_swap
        ;;
        engrave)
            _tomb_sym
        ;;
        exhume)
            _tomb_assym
            _tomb_sym
            _tomb_pwd
        ;;
        forge)
            _tomb_forge
            _tomb_assym
            _tomb_sym
            _tomb_swap
        ;;
        help)
        ;;
        index)
        ;;
        list)
            _tomb_list
        ;;
        lock)
            _tomb_lock
            _tomb_assym
            _tomb_sym
            _tomb_pwd
        ;;
        mount|open)
            _tomb_open
            _tomb_assym
            _tomb_hooks
            _tomb_pwd
            _tomb_swap
            _tomb_sym
        ;;
        passwd|setkey)
            _tomb_assym
            _tomb_sym
            _tomb_swap
            _tomb_pwd
            _tomb_pwd_old
        ;;
        ps)
        ;;
        resize)
            _tomb_assym
            _tomb_sym
            _tomb_pwd
            _tomb_size
            _tomb_swap
        ;;
        search)
            # allow path completion?
        ;;
        slam)
        ;;
    esac
}


function _tomb_assym {
    _arguments \
        "-g[use a GnuPG key to encrypt a tomb key]" \
        "-r[provide GnuPG recipients (separated by comma)]" \
        "-R[provide GnuPG hidden recipients (separated by comma)]"
}

function _tomb_forge {
    _arguments \
        "-o[specify cipher algo for key generation]" \
        "--use-urandom[if /dev/random is to slow]" \
        "--kdf=[specify the KDF method]"
}

function _tomb_hooks {
    _arguments \
        '(-)'{-n,--nohook}'[do not process hooks found in tomb]'
}

function _tomb_list {
    _arguments \
        "--get-mount-point[Only print mount point of tombs]"
}

function _tomb_lock {
    _arguments \
        "-o[specify cipher specification]"
}

function _tomb_open {
    _arguments \
        "-o[specify mount options]" \
        "-p[preserve ownership of files in tomb]" \
        "--preserve-ownership[of files in tomb]"
}

function _tomb_pwd {
    _arguments \
        "--tomb-pwd=[Use string as password when needed on tomb]"
}

function _tomb_pwd_old {
    _arguments \
        "--tomb-old-pwd=[Use string as old password when needed on tomb]"
}

function _tomb_size {
    _arguments \
        '(-)'{-s,--size}'[size of tomb (MB)]'
}

function _tomb_swap {
    _arguments \
        "--ignore-swap[ignore existing swap partitions/files]"
}

function _tomb_sym {
    _arguments \
        "-k[the key file to use]"
}
